macro make 
for pam in [*]
  t = $word([pam],1,1,'/')
  if $index([t],'pams')<1 then
    p = pams/[pam]
  else
    p = [pam]
  endif 
  exec makes pam=[p]
endfor
return
**************************************************
macro makes pam=pams/tpc/tpt makefile=$STAR_ROOT/mgr/Makefile
cut = ' \( -type d -a ! -name CVS -a ! -name idl _
                   -a ! -name exa -a ! -name inc _
                   -a ! -name "src/*" -a ! -name doc _
                   -a ! -name . -a ! -name wrk \) -print'
list = ''
if $fexist([pam])=1 then
list = $shell('find '//[pam]//[cut]);
endif
listr= $shell('find $STAR_ROOT/'//[pam]//[cut]);
exec load [list] [listr] 
ami/list
return
**************************************************
macro load 
n1 = $words([1],' ')
n2 = $words([2],' ')
i1 = 1 
cwd1 = '????'
if [n1]>0 then
  cwd1 = $word([1],[i1],1)
endif
do i2 = 1, [n2]
  cwd2 = $word([2],[i2],1)
  ind  = $index([cwd2],[cwd1]) 
  if [ind]=0 then
    exec ld_standard [cwd2]
  elseif [i1]<=[n1] then 
    exec ld_private  [cwd1]
    i1 = [i1]+1
    cwd1 = $word([1],[i1],1)
  endif
enddo
do i= [i1], [n1]
  cwd1 = $word([1],[i],1)
  exec ld_private  [cwd1]
enddo 
return
**************************************************
macro ld_standard
  pam = [1]
  n = $words([pam],'/') 
  p = $word([pam],[n],[n],'/')
  slib = $STAR_LIB/[p].sl
  sl = $fexist([slib])
  if [sl]=1 then
     message  '================ Load standard shared library:' [slib] '================'
     gexec [slib]; for/call [p]_init;
  endif
return
**************************************************
macro ld_private
  pam = [1]
  n = $words([pam],'/') 
  p = $word([pam],[n],[n],'/')
    list = $shell('cd '//[pam]//';ls *.idl* *.g')
    lidm    = $index([list],'.idl') 
    lg      = $index([list],'.g')
    if [lidm]>0 .or. [lg]>0 then
      shell make -f $STAR_ROOT/mgr/Makefile -C [pam]
      slib = lib/$STAR_SYS/[p].sl
      sl = $fexist([slib])
      if [sl]=1 then
         message  '================ Load private shared library:' [slib] '================'
         gexec [slib]; for/call [p]_init;
      endif
    else
         message 'No modules in ' [pam]
    endif
return
endkumac
**************************************************
macro gload
on error exitm
*call  csrmsl($quote([1]))
*shell make [1].sl
*call  [1]
gexec [1]
exitm
