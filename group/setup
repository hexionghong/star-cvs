#!/bin/csh -f

# Help
if ($#argv == 0 || $#argv > 3) then
    /bin/cat <<EOF

 Usage is:
   % setup 'Package' {version}

 where packages stands for one of
   debug|DEBUG                      switch the environment to the debug version of 
                                    the STAR software
   nodebug|NODEBUG                  switch the environment to the nodebug (optimized) 
                                    of the STAR software
   64bits|64b|32bits|32b            Switch to 64 or 64 bits env

   gprof|GRPOF                      enables/disables gprof profiling environment
   nogprof|NOGPROF                  non-optimized version will be implied when enabled


   Grid|OSG                         Setup Grid environment
   root|ROOT     {version}          load or change root version
   cern|CERN     {version} {InstallRoot} 
                                    Re-define CERN related ENV variables

   parasoft|PARASOFT|insure|ISURE   load the Insure++ toolkit environment
   gcc           {version}          set environment to use gcc (or alternative version)
   icc|ifc|ifort {version}          set environment to use Intel compilers
   PGI|pgi                          set PGI path for using its compiler

   alt|noalt                        adds/remove OPTSTAR/alt from path

EOF
    exit 0
endif

set pkg = $1; shift;
set version  = 0;
set location = 0;
if ( $#argv > 0 ) set version  = $1
if ( $#argv > 1 ) set location = $2

switch ($pkg)
    case "64bits":
    case "64b":
      setenv USE_64BITS 1
      #source $GROUP_DIR/STAR_SYS
      unsetenv OPTSTAR
      unsetenv QTDIR
      unsetenv LD_LIBRARY_PATH 
      #if ( $?SAVED_PATH ) then
      #	set path=($SAVED_PATH)
      #endif
      setenv FLOGIN 1 
      setenv SILENT 1
      source $GROUP_DIR/star_login.csh 
      unsetenv SILENT
      #source $GROUP_DIR/group_env.csh
      breaksw

    case "32bits":
    case "32b":
      setenv USE_64BITS 0
      #source $GROUP_DIR/STAR_SYS
      unsetenv OPTSTAR
      unsetenv QTDIR
      unsetenv LD_LIBRARY_PATH
      #if ( $?SAVED_PATH ) then
      #	set path=($SAVED_PATH)
      #endif
      #source $GROUP_DIR/group_env.csh 
      setenv FLOGIN 1 
      setenv SILENT 1
      source $GROUP_DIR/star_login.csh 
      unsetenv SILENT
      breaksw


    case "PGI":
    case "pgi":
      # PGI compiler - this may be used
      if ( ! $?PGI) then
        # should be defined but does not hurt for now to protect
        if ( ! $?USE_64BITS ) then
	   setenv USE_64BITS 0
	endif
	if ( $USE_64BITS == 1 ) then
	   set x="/usr/pgi64 /usr/pgi"
	else
	   set x="/usr/pgi"
	endif

	foreach d ($x)
	    if ( -d $d ) then
		#echo "Setting PGI to $d"
		setenv PGI $d
		break
	    endif
	end
      endif
      if ( $?PGI ) then
	if ( ! -d $PGI/linux86/bin && -e $PGI/linux86 ) then
	    set version=`/bin/ls  $PGI/linux86/ | /usr/bin/tail -1`
	    setenv PGI_V linux86/$version
	else
	    setenv PGI_V linux86
	endif
	setenv PATH `${GROUP_DIR}/dropit -p $PGI/$PGI_V/bin -p $PATH`
	if ( -d $PGI/$PGI_V/man) then
	    setenv MANPATH `${GROUP_DIR}/dropit -p ${MANPATH} -p ${PGI}/${PGI_V}/man`
	else
	    setenv MANPATH `${GROUP_DIR}/dropit -p ${MANPATH} -p ${PGI}/man`
	    #alias  pgman 'man -M $PGI/man'
	endif
	setenv PGILIB  ${PGI}/${PGI_V}/lib
	setenv LM_LICENSE_FILE $PGI/license.dat
      endif
      breaksw



    case "OSG":
    case "Grid":
	# Support for OSG - Try to locate things from OSG including Worker Node
	if ( ! $?OSG ) then 
	    if ( -e /opt ) then
		set LOSG=`/bin/ls -d /opt/* | /bin/grep OSG | /usr/bin/tail -1`
		if ( "$LOSG" != "") setenv OSG $LOSG
	    endif
	endif
	if ( $?OSG ) then
	    if ( -e $OSG/setup.csh ) then
		# there will alos be a java version coming along but
		# it may be defined prior from /usr/java
		source $OSG/setup.csh
		setenv SAVED_PATH `echo $PATH | /bin/sed "s/:/ /g"`
	    endif
	else
	    # Unfortunately, the WN package loads the whole blabla with
	    # java, python and even perl all ...
	    if ( $?WNOSG ) then
		if ( -e $WNOSG/setup.csh ) then
		    setenv GSAVED_PATH   $PATH
		    setenv GSAVED_LDPATH $LD_LIBRARY_PATH
		    # trash the path
		    setenv PATH  /bin:/usr/bin
		    # trash LD
		    unsetenv LD_LIBRARY_PATH
		    # load definitions
		    source $WNOSG/setup.csh
		    # redefine path and ld path
		    setenv PATH             ${GSAVED_PATH}:${PATH}
		    setenv LD_LIBRARY_PATH  ${GSAVED_LDPATH}:${LD_LIBRARY_PATH}
	        endif
	    endif
        endif
	breaksw



    # Root setup
    case "root":
    case "ROOT":
	if (-f $GROUP_DIR/rootenv.csh) then
	    if ( -x $GROUP_DIR/dropit) then
		setenv MANPATH         `$GROUP_DIR/dropit -p ${MANPATH} ROOT`
		setenv PATH            `$GROUP_DIR/dropit -p ${PATH} ROOT`
		setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p ${LD_LIBRARY_PATH} ROOT`
	    endif
	    if ($version != 0) setenv ROOT_LEVEL $version
	    source $GROUP_DIR/rootenv.csh
	endif
	breaksw

    # CERN in case of alternate CERN_ROOT
    case "CERN":
    case "cern":
	if ( $location != 0 ) then
	    # use the specified location
	    setenv CERN $location
        else
	    # try to determine the location according to pre-2010 logic
	    if ( ! $?USE_64BITS ) then
		# this is also defined in STAR_SYS but ensuring
		# a defula tis needed for the test below
		setenv USE_64BITS 0
	    endif
	    # some of the path below are historical
	    if ( $USE_64BITS == 1) then
		set x="/cern64 /cern ${AFS_RHIC}/asis/@sys/cern64 ${AFS_RHIC}/asis/@sys/cern /usr/local/cern64 /usr/local/cern"
	    else
		set x="/cern ${AFS_RHIC}/asis/@sys/cern /usr/local/cern"
	    endif
	    foreach d ($x)
		if ( -e $d ) then
		    setenv CERN $d
		    break
		endif
	    end
	    if ( ! $?CERN ) then
		echo "Could not find a valid path for defining CERN, default to /cern"
		setenv CERN /cern
	    endif
        endif
	if ( $version != 0 ) then
	    # if specified, use specific version
	    if ( -e $CERN/$version ) then
		setenv CERN_LEVEL $version 
	    endif
	else
	    # define to pro if not defined
	    if ( ! $?CERN_LEVEL ) then
		setenv CERN_LEVEL pro
	    endif
	endif
	# check of existence will be done in the star login
	setenv CERN_ROOT $CERN/$CERN_LEVEL
	breaksw

    # Parasoft/Isure++ software
    case "insure":
    case "INSURE":
    case "parasoft":
    case "PARASOFT":
	if (-f $GROUP_DIR/parasoftenv.csh) source $GROUP_DIR/parasoftenv.csh
	breaksw


    # use of the Intel compiler
    case "icc":
    case "ifc":
    case "ifort":
	unsetenv GPROF  # don't know about -qp ; perhaps later
	if ( -f $GROUP_DIR/intelcc.csh  )  source $GROUP_DIR/intelcc.csh $version
	if ( $?optstar )  setenv  OPTSTAR  $optstar
	if ( $?xoptstar ) setenv XOPTSTAR  $xoptstar
	if ( $?STARCMPL && -x $GROUP_DIR/dropit ) then
	    setenv PATH            `$GROUP_DIR/dropit $STAR_HOST_SYS`
	    setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p $LD_LIBRARY_PATH $STAR_HOST_SYS`
	    unsetenv QTDIR
	endif 
	setenv STARCMPL "icc"; starver $STAR_LEVEL
	breaksw


    # Alternative or restore of gcc version
    case "gcc":
      unsetenv QTDIR
      if ( -x $GROUP_DIR/dropit ) then
	setenv PATH            `$GROUP_DIR/dropit alt opt/gcc`
	setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p $LD_LIBRARY_PATH $OPTSTAR/alt opt/gcc`
      endif

      if ( $version != 0 ) then
        set TEST=`/usr/bin/test -d $OPTSTAR/alt && /bin/ls -ld $OPTSTAR/alt/lib/gcc-lib/*/$version/`
        if ("$TEST" == "") then
	  set TEST=`/usr/bin/test -d /opt/gcc/$version && echo "/opt/gcc/$version"`
	  if ( "$TEST" == "") then
	    echo "There are no alternate installation of gcc with version $version in $OPTSTAR/alt nor /opt/gcc/"
	  else
	    set BP=/opt/gcc/$version
	  endif
        else
	  set BP=$OPTSTAR/alt
        endif
	  
	if ( "$TEST" != "") then
	  setenv PATH ${BP}/bin:$PATH
	  if ( $?USE_64BITS ) then
	    if ( $USE_64BITS == 1 ) then
		setenv LD_LIBRARY_PATH ${BP}/lib64:${BP}/lib:$LD_LIBRARY_PATH
	    else
		setenv LD_LIBRARY_PATH ${BP}/lib:${BP}/lib64:$LD_LIBRARY_PATH		
	    endif
	  else
	    setenv LD_LIBRARY_PATH ${BP}/lib:$LD_LIBRARY_PATH
          endif
        endif
      endif
      if ( $?optstar )  setenv  OPTSTAR  $optstar
      if ( $?xoptstar ) setenv XOPTSTAR $xoptstar
      if ( $?STARCMPL && -x $GROUP_DIR/dropit ) then
	setenv PATH            `$GROUP_DIR/dropit $STAR_HOST_SYS`
	setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p $LD_LIBRARY_PATH $STAR_HOST_SYS`
      endif 
      setenv STARCMPL "gcc"; starver $STAR_LEVEL
      breaksw

    # alternatively, add a plugin for the alt tree
    case "alt":
      if ( -x $GROUP_DIR/dropit ) then
	setenv PATH            `$GROUP_DIR/dropit alt`
	setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p $LD_LIBRARY_PATH $OPTSTAR/alt`
      endif
      setenv PATH $OPTSTAR/alt/bin:$PATH
      setenv LD_LIBRARY_PATH $OPTSTAR/alt/lib:$LD_LIBRARY_PATH
      breaksw

    case "noalt":
      if ( -x $GROUP_DIR/dropit ) then
	setenv PATH            `$GROUP_DIR/dropit alt`
	setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p $LD_LIBRARY_PATH $OPTSTAR/alt`
      endif
      breaksw


    # debug / nodebug
    case "nodebug":
    case "NODEBUG":
	unsetenv GPROF
	setenv NODEBUG yes
	starver $STAR_LEVEL
	breaksw
    case "debug":
    case "DEBUG":
	unsetenv NODEBUG yes
	if ( -x $GROUP_DIR/dropit ) then
	    setenv PATH            `$GROUP_DIR/dropit BIN`
	    setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p $LD_LIBRARY_PATH LIB`
	endif
	starver $STAR_LEVEL
	breaksw


    # grpof / nogrpof
    case "gprof":
    case "GPROF":
	setenv GPROF "yes"
	unsetenv NODEBUG
	starver $STAR_LEVEL
	breaksw

    case "nogprof":
    case "NOGPROF":
	unsetenv GPROF       
	if ( -x $GROUP_DIR/dropit ) then
	    setenv PATH            `$GROUP_DIR/dropit GBIN`
	    setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p $LD_LIBRARY_PATH GLIB`
	endif
	starver $STAR_LEVEL
	breaksw



    #
    # kind of obsolete but may re-add later
    #
    case "g4":
    case "G4":
         #=====#
	if (-f $GROUP_DIR/g4env.csh) source $GROUP_DIR/G4env.csh
	breaksw

    case "cc4":
    case "CC4":
         #=====#
	if ($?USE_CC5 != 0) unsetenv USE_CC5
        if ( -x $GROUP_DIR/dropit) then
          setenv PATH `$GROUP_DIR/dropit WS5.0`
          setenv PATH `$GROUP_DIR/dropit .sun4x_56_CC5`
          setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p $LD_LIBRARY_PATH 5.0`
          setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p $LD_LIBRARY_PATH .sun4x_56_CC5`
        endif
	setenv USE_CC4; starver $STAR_LEVEL
	breaksw
    case "cc5":
    case "CC5":
         #=====#
	if ($?USE_CC4 != 0) unsetenv USE_CC4
        if ( -x $GROUP_DIR/dropit) then
          setenv PATH `$GROUP_DIR/dropit SUNWspro`
          setenv PATH `$GROUP_DIR/dropit .sun4x_56`
          setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p $LD_LIBRARY_PATH SUNWspro`
          setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p $LD_LIBRARY_PATH .sun4x_56`
        endif
	starver $STAR_LEVEL
	breaksw

    case "Dsv":
    case "dsv":
    case "DSV":
	if (-r $AFS_RHIC/star/packages/Dsv/Dsvenv.csh) source $AFS_RHIC/star/packages/Dsv/Dsvenv.csh
      breaksw
    case "KCC":
    case "kcc":
      setenv USE_KCC;  starver $STAR_LEVEL
      breaksw
    default:
	echo "Setup for [$pkg] is not defined"
      breaksw
endsw
