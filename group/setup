#!/usr/local/bin/tcsh -f

#set packages = "objy|OBJY|Objy root|ROOT parasoft|PARASOFT insure|ISURE sniff g4 cc4|CC4 cc5|CC5 Dsv kcc|KCC debug|DEBUG nodebug|NODEBUG"

if ($#argv == 0 || $#argv > 2) then
    cat <<EOF

 Usage is:
   % setup 'Package' {version}

 where packages stands for one of
   debug|DEBUG                      switch the environment to the debug version of 
                                    the STAR software
   nodebug|NODEBUG                  switch the environment to the nodebug (optimized) 
                                    of the STAR software

   root|ROOT  {version}             load or change root version
   parasoft|PARASOFT|insure|ISURE   load the Insure++ toolkit environment

   gcc        {version}             set environment to use gcc (or alternative version)
   icc|ifc                          set environment to use Intel compilers

EOF
    exit 0
endif

set pkg = $1; shift;
set version = 0;
if ( $#argv > 0 ) set version = $1

switch ($pkg)
    # Root setup
    case "root":
    case "ROOT":
	if (-f $GROUP_DIR/rootenv.csh) then
	    if ( -x $GROUP_DIR/dropit) then
		setenv MANPATH         `$GROUP_DIR/dropit -p ${MANPATH} ROOT`
		setenv PATH            `$GROUP_DIR/dropit -p ${PATH} ROOT`
		setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p ${LD_LIBRARY_PATH} ROOT`
	    endif
	    if ($version != 0) setenv ROOT_LEVEL $version
	    source $GROUP_DIR/rootenv.csh
	endif
	breaksw


    # Parasoft/Isure++ software
    case "insure":
    case "INSURE":
    case "parasoft":
    case "PARASOFT":
	if (-f $GROUP_DIR/parasoftenv.csh) source $GROUP_DIR/parasoftenv.csh
	breaksw


    # use of the Intel compiler
    case "icc":
    case "ifc":
	if ( -f $GROUP_DIR/intelcc.csh  )  source $GROUP_DIR/intelcc.csh 
	if ( $?optstar ) setenv  OPTSTAR  $optstar
	if ( $?optstar ) setenv XOPTSTAR $xoptstar
	setenv STARCMPL "icc"; starver $STAR_LEVEL
	breaksw


    # Alternative or restore of gcc version
    case "gcc":
      if ( -x $GROUP_DIR/dropit ) then
	setenv PATH            `$GROUP_DIR/dropit alt`
	setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p $LD_LIBRARY_PATH $OPTSTAR/alt`
      endif

      if ( $version != 0 ) then
        set TEST=`/bin/ls -ld $OPTSTAR/alt/lib/gcc-lib/*/$version/`
        if ("$TEST" == "") then
	  echo "There are no alternate installation of gcc with version $version in $OPTSTAR/alt"
        else
	  setenv PATH $OPTSTAR/alt/bin:$PATH
	  setenv LD_LIBRARY_PATH $OPTSTAR/alt/lib:$LD_LIBRARY_PATH
        endif
      endif
      if ( $?optstar ) setenv  OPTSTAR  $optstar
      if ( $?optstar ) setenv XOPTSTAR $xoptstar
      setenv STARCMPL "gcc"; starver $STAR_LEVEL
      breaksw


    case "nodebug":
    case "NODEBUG":
      setenv NODEBUG yes; starver $STAR_LEVEL
      breaksw
    case "debug":
    case "DEBUG":
      unsetenv NODEBUG yes;
      if ( -x $GROUP_DIR/dropit ) then
	setenv PATH            `$GROUP_DIR/dropit BIN`
	setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p $LD_LIBRARY_PATH LIB`
      endif
      starver $STAR_LEVEL
      breaksw





    case "objy":
    case "OBJY":
    case "Objy":
         #=====#
	if (`uname -s` == "SunOS" && `hostname` != "rcf.rhic.bnl.gov" && ! ${?OBJY_HOME} ) source $GROUP_DIR/ObjySetup.csh
	breaksw


    case "sniff":
    case "SNIFF":
         #=====#
	if (-f $GROUP_DIR/sniffenv.csh) source $GROUP_DIR/sniffenv.csh
	breaksw
    case "g4":
    case "G4":
         #=====#
	if (-f $GROUP_DIR/g4env.csh) source $GROUP_DIR/G4env.csh
	breaksw
    case "cc4":
    case "CC4":
         #=====#
	if ($?USE_CC5 != 0) unsetenv USE_CC5
        if ( -x $GROUP_DIR/dropit) then
          setenv PATH `$GROUP_DIR/dropit WS5.0`
          setenv PATH `$GROUP_DIR/dropit .sun4x_56_CC5`
          setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p $LD_LIBRARY_PATH 5.0`
          setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p $LD_LIBRARY_PATH .sun4x_56_CC5`
        endif
	setenv USE_CC4; starver $STAR_LEVEL
	breaksw
    case "cc5":
    case "CC5":
         #=====#
	if ($?USE_CC4 != 0) unsetenv USE_CC4
        if ( -x $GROUP_DIR/dropit) then
          setenv PATH `$GROUP_DIR/dropit SUNWspro`
          setenv PATH `$GROUP_DIR/dropit .sun4x_56`
          setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p $LD_LIBRARY_PATH SUNWspro`
          setenv LD_LIBRARY_PATH `$GROUP_DIR/dropit -p $LD_LIBRARY_PATH .sun4x_56`
        endif
	starver $STAR_LEVEL
	breaksw
    case "Dsv":
    case "dsv":
    case "DSV":
	if (-r $AFS_RHIC/star/packages/Dsv/Dsvenv.csh) source $AFS_RHIC/star/packages/Dsv/Dsvenv.csh
      breaksw
    case "KCC":
    case "kcc":
      setenv USE_KCC;  starver $STAR_LEVEL
      breaksw
    default:
	echo "Setup for $pkg is not defined"
      breaksw
endsw
