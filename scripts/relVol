#!/bin/csh

#
# Creates a .Version file and ghe CVMFS release file
# -c to clean all previous version files before a new release (CVMFS only)
#
#echo $PWD
#echo $PWD | grep '/\/afs/'
#echo $PWD | grep '/\/cvmfs/'

onintr ERROR
unset noclobber

set ISAFS   = `echo $PWD | grep '/afs/'`
set ISCVMFS = `echo $PWD | grep '/cvmfs/'`

if ( "$ISAFS" == "" && "$ISCVMFS" == "" ) then
    echo "Not sure what to do to release $PWD [$ISAFS][$ISCVMFS]"
    exit
endif

foreach file ( temp_gccflags.c ) 
    if ( -e $file ) then
	/bin/rm -f $file || goto ERROR
    endif
end

if ( "$ISAFS" != "" ) then
    # in AFS land, we need to also release the base
    # volume that may contain links but we do not do 
    # this automatically ... 
    echo "Releasing volume $PWD"
    if ( ! -d ".log") then
	# Lidia's way is to have one more level - support that
	# as well
	if ( -d "../.log") then
	    echo "   Sub-volume detected ../.log/ release underway"
	    /bin/date >../.log/afs.release  || goto ERROR
	else
	    echo "Could not find a .log directory"
	endif
    else
	/bin/date >.log/afs.release

	# more complicated than a single level volume due to "dev"
	if ( -d ".$STAR_HOST_SYS/.log/" ) then
	    echo "   Sub-volume .$STAR_HOST_SYS release underway"
	    /bin/date >.$STAR_HOST_SYS/.log/afs.release || goto ERROR
	endif
    endif
    set BPath=`echo $PWD | sed 's/\/packages.*//'`
    set Test=`echo $BPath | grep 'afs\/\.rhic'`
    #echo $BPath
    if ( "$Test" != "" && -d "$BPath/packages/.log" ) then
	echo "You may also date >$BPath/packages/.log/afs.release as needed"
    endif
else
    echo "Releasing volume $PWD"
    set vdir=".Version"

    if ( "$1" == "-c" ) then
	if ( -d $vdir ) then
	    /bin/rm -fr $vdir
	endif
    endif
    if ( ! -d $vdir ) then
	/usr/bin/mkdir $vdir
    endif

    /bin/date >$vdir/.`date +%Y.%m.%d.%H%M` && echo "" > CVMFSRELEASE
endif

exit

ERROR:
  echo "Are you sure you have access to `pwd` ??"
  if ( "$ISAFS" != "") then
    echo "Check if you have a valid AFS token or in the proper area"
  endif
